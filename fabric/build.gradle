plugins {
    id 'fabric-loom' version "0.10-SNAPSHOT"
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

minecraft {
    mixin.defaultRefmapName = 'infinitemusic.refmap.json'
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    maven { url "https://maven.shedaniel.me/" }
}

archivesBaseName = project.mod_id + "-" + project.mod_version + "-" + project.minecraft_version + "-fabric"

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    // Mappings
    mappings loom.officialMojangMappings()
    //    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    // Fabric APIs and Loader
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation project(":common")

    modImplementation("me.shedaniel.cloth:cloth-config-fabric:6.1.48") {
        exclude(group: "net.fabricmc.fabric-api")
    }
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"
    from project(":common").sourceSets.main.resources

    filesMatching("fabric.mod.json") {
        expand "mod_id": project.mod_id,
                "mod_version": project.mod_version,
                "mod_title": project.mod_title,
                "mod_description": project.mod_description,
                "mod_authors": project.mod_authors,
                "mod_license": project.mod_license,
                "mod_icon_path": project.mod_icon_path,
                "minecraft_version_range_fabric": project.minecraft_version_range_fabric
    }
}

tasks.withType(JavaCompile) {
    source(project(":common").sourceSets.main.allSource)
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }
}

if (System.getenv("CURSEFORGE")) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE")
        project {
            id = "291713"
            addGameVersion "${project.minecraft_version}"
            changelogType = "markdown"
            changelog = ""
            releaseType = "beta"
            mainArtifact(jar) {
                displayName = "[Fabric]Infinite-Music-${project.mod_version}-${project.minecraft_version}"
                relations {
                    requiredDependency "fabric-api"
                    optionalDependency "cloth-config"
                }
            }
        }
    }
}